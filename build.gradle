import groovy.text.SimpleTemplateEngine

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
    }
}

plugins {
    id('maven-publish')
    id('signing')
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

project.ext {
    libraryVersion = "4.4.3"
    androidXSQLiteVersion = "2.1.0"
    androidNdkVersion = "23.0.7599858"
    mavenLocalRepositoryPrefix = "file://"
    if(project.hasProperty('publishLocal') && publishLocal.toBoolean()){
        mavenSnapshotRepositoryUrl = "outputs/snapshot"
        mavenReleaseRepositoryUrl = "outputs/release"
    } else {
        mavenLocalRepositoryPrefix = ""
        mavenSnapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots"
        mavenReleaseRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
    }
    if(project.hasProperty('publishSnapshot') && publishSnapshot.toBoolean()){
        mavenVersionName = "${libraryVersion}-SNAPSHOT"
    } else {
        mavenVersionName = "${libraryVersion}"
    }
    if(project.hasProperty('nexusUsername')){
        nexusUsername = "${nexusUsername}"
    }
    if(project.hasProperty('nexusPassword')){
        nexusPassword = "${nexusPassword}"
    }
    nexusUsername = project.hasProperty('nexusUsername') ? "${nexusUsername}" : ""
    nexusPassword = project.hasProperty('nexusPassword') ? "${nexusPassword}" : ""
    nexusStagingProfileId = project.hasProperty('nexusStagingProfileId') ? "${nexusStagingProfileId}" : ""
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://oss.sonatype.org/")
            snapshotRepositoryUrl = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            username = "${rootProject.ext.nexusUsername}"
            password = "${rootProject.ext.nexusPassword}"
            stagingProfileId = "${rootProject.ext.nexusStagingProfileId}"
        }
        localNexus {
            useStaging = false
            nexusUrl = uri("http://localhost:8081/")
            snapshotRepositoryUrl = uri("http://localhost:8081/repository/maven-snapshots/")
            username = "${rootProject.ext.nexusUsername}"
            password = "${rootProject.ext.nexusPassword}"
            stagingProfileId = ""
        }
    }
}

task generateReadMe {
    def readme = new File("README.md")
    def engine = new SimpleTemplateEngine()
    def reader = new FileReader("README.md.template")
    def binding = [
                    libraryVersion: project.ext.libraryVersion,
                    androidXSQLiteVersion: project.ext.androidXSQLiteVersion,
                    androidNdkVersion: project.ext.androidNdkVersion
                  ]
    def template = engine.createTemplate(reader).make(binding)
    readme.write(template.toString())
}

project.afterEvaluate {
    build.dependsOn generateReadMe
}